buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.3.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'com.novoda.bintray-release' // must be applied after your artifact generating plugin (eg. java / com.android.library)

dependencies {
    compile('commons-lang:commons-lang:2.6') {
        exclude group: 'commons-logging'
    }
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.android.support:support-v13:23.2.1'
}

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        versionName "1.9.0"
        minSdkVersion 14
        targetSdkVersion 23
    }
}

publish {
    artifactId= 'utils'
    userOrg = 'wordpress-mobile'
    groupId = 'org.wordpress'
    uploadName = 'utils'
    description = 'Library for common utilities used by WordPress-Android'
    publishVersion = '1.9.0'
    licences = ['MIT', 'GPL']
    website = 'https://github.com/wordpress-mobile/WordPress-Utils-Android'
    dryRun = 'false'
    autoPublish = 'true'
}

android.libraryVariants.all { variant ->

    task("generate${variant.name}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        classpath = files(variant.javaCompile.classpath.files, android.getBootClasspath())

        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
        }
        exclude '**/R.java'
    }
}
